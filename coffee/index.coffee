window.CONFIG = 
  FIELD_MIN: 2
  FIELD_MAX: 3

window.CONSTS =
  NOT_MIMIC: 0
  MIMIC: 1
  EQUIP: 2
  MONEY: 3
  COMMODITY: 4

window.COLORS =
  RED: 1
  BLUE: 2
  BLACK: 3

window.DIRECTIONS =
  UP: 1
  DOWN: 2
  LEFT: 3
  RIGHT: 4

window.CONDS = 
  'Zzz...':
    0: 'Zzz...'
  'ミミック2匹以上が縦か横で隣あった位置に':
    1100: 'いる'
    1110: 'はいない'
  '上の宝箱は':
    710: 'ミミックだ'
    720: 'ミミックじゃない'
    732: '装備品が入っている'
    733: 'お金が入っている'
    734: '消費アイテムが入っている'
    742: '装備品が入っていない'
    743: 'お金が入っていない'
    744: '消費アイテムが入っていない'
  '下の宝箱は':
    810: 'ミミックだ'
    820: 'ミミックじゃない'
    832: '装備品が入っている'
    833: 'お金が入っている'
    834: '消費アイテムが入っている'
    842: '装備品が入っていない'
    843: 'お金が入っていない'
    844: '消費アイテムが入っていない'
  '左の宝箱は':
    910: 'ミミックだ'
    920: 'ミミックじゃない'
    932: '装備品が入っている'
    933: 'お金が入っている'
    934: '消費アイテムが入っている'
    942: '装備品が入っていない'
    943: 'お金が入っていない'
    944: '消費アイテムが入っていない'
  '右の宝箱は':
    1010: 'ミミックだ'
    1020: 'ミミックじゃない'
    1032: '装備品が入っている'
    1033: 'お金が入っている'
    1034: '消費アイテムが入っている'
    1042: '装備品が入っていない'
    1043: 'お金が入っていない'
    1044: '消費アイテムが入っていない'
  '赤い宝箱のなかに':
    10: 'ミミックがいるよ'
    20: 'ミミックはいないよ'
  '赤い宝箱に':
    30: 'ミミックは0匹いる'
    31: 'ミミックは1匹いる'
    32: 'ミミックは2匹いる'
    33: 'ミミックは3匹いる'
    34: 'ミミックは4匹いる'
    35: 'ミミックは5匹いる'
    36: 'ミミックは6匹いる'
    37: 'ミミックは7匹いる'
    38: 'ミミックは8匹いる'
    39: 'ミミックは9匹いる'
  '青い宝箱のなかに':
    110: 'ミミックがいるよ'
    120: 'ミミックはいないよ'
  '青い宝箱に':
    130: 'ミミックは0匹いる'
    131: 'ミミックは1匹いる'
    132: 'ミミックは2匹いる'
    133: 'ミミックは3匹いる'
    134: 'ミミックは4匹いる'
    135: 'ミミックは5匹いる'
    136: 'ミミックは6匹いる'
    137: 'ミミックは7匹いる'
    138: 'ミミックは8匹いる'
    139: 'ミミックは9匹いる'
  '黒い宝箱のなかに':
    210: 'ミミックがいるよ'
    220: 'ミミックはいないよ'
  '黒い宝箱に':
    230: 'ミミックは0匹いる'
    231: 'ミミックは1匹いる'
    232: 'ミミックは2匹いる'
    233: 'ミミックは3匹いる'
    234: 'ミミックは4匹いる'
    235: 'ミミックは5匹いる'
    236: 'ミミックは6匹いる'
    237: 'ミミックは7匹いる'
    238: 'ミミックは8匹いる'
    239: 'ミミックは9匹いる'
  '一番上の列':
    310: 'にミミックがいるよ'
    320: 'にミミックはいないよ'
    10002: 'のどこかに装備品が入っている'
    10003: 'のどこかにお金が入っている'
    10004: 'のどこかに消費アイテムが入っている'
    10012: 'のどこにも装備品は入っていない'
    10013: 'のどこにもお金は入っていない'
    10014: 'のどこにも消費アイテムは入っていない'
  '一番下の列':
    410: 'にミミックがいるよ'
    420: 'にミミックはいないよ'
    20002: 'のどこかに装備品が入っている'
    20003: 'のどこかにお金が入っている'
    20004: 'のどこかに消費アイテムが入っている'
    20012: 'のどこにも装備品は入っていない'
    20013: 'のどこにもお金は入っていない'
    20014: 'のどこにも消費アイテムは入っていない'
  '一番左の列':
    510: 'にミミックがいるよ'
    520: 'にミミックはいないよ'
    30002: 'のどこかに装備品が入っている'
    30003: 'のどこかにお金が入っている'
    30004: 'のどこかに消費アイテムが入っている'
    30012: 'のどこにも装備品は入っていない'
    30013: 'のどこにもお金は入っていない'
    30014: 'のどこにも消費アイテムは入っていない'
  '一番右の列':
    610: 'にミミックがいるよ'
    620: 'にミミックはいないよ'
    40002: 'のどこかに装備品が入っている'
    40003: 'のどこかにお金が入っている'
    40004: 'のどこかに消費アイテムが入っている'
    40012: 'のどこにも装備品は入っていない'
    40013: 'のどこにもお金は入っていない'
    40014: 'のどこにも消費アイテムは入っていない'
  '上の列と下の列は':
    1210: '上の方がミミックが多い'
    1220: '下の方がミミックが多い'
    1230: 'ミミックの数は同じ'
  '左の列と右の列は':
    1310: '左の方がミミックが多い'
    1320: '右の方がミミックが多い'
    1330: 'ミミックの数は同じ'
  '赤宝箱と':
    1410: '青宝箱は、赤宝箱の方がミミックが多い'
    1420: '青宝箱は、青宝箱の方がミミックが多い'
    1421: '青宝箱のミミックの数は同じ'
    1430: '黒宝箱は、赤宝箱の方がミミックが多い'
    1440: '黒宝箱は、黒宝箱の方がミミックが多い'
    1441: '黒宝箱のミミックの数は同じ'
  '青宝箱と':
    1510: '赤宝箱は、青宝箱の方がミミックが多い'
    1520: '赤宝箱は、赤宝箱の方がミミックが多い'
    1521: '赤宝箱のミミックの数は同じ'
    1530: '黒宝箱は、青宝箱の方がミミックが多い'
    1540: '黒宝箱は、黒宝箱の方がミミックが多い'
    1541: '黒宝箱のミミックの数は同じ'
  '黒宝箱と':
    1610: '赤宝箱は、黒宝箱の方がミミックが多い'
    1620: '赤宝箱は、赤宝箱の方がミミックが多い'
    1621: '赤宝箱のミミックの数は同じ'
    1630: '青宝箱は、黒宝箱の方がミミックが多い'
    1640: '青宝箱は、青宝箱の方がミミックが多い'
    1641: '青宝箱のミミックの数は同じ'
  '私は':
    1700: 'ミミックじゃない'
  '赤い宝箱の':
    1811: '上にミミックがいる'
    1821: '上にミミックはいない'
    1812: '下にミミックがいる'
    1822: '下にミミックはいない'
    1813: '左にミミックがいる'
    1823: '左にミミックはいない'
    1814: '右にミミックがいる'
    1824: '右にミミックはいない'
  '青い宝箱の':
    1911: '上にミミックがいる'
    1921: '上にミミックはいない'
    1912: '下にミミックがいる'
    1922: '下にミミックはいない'
    1913: '左にミミックがいる'
    1923: '左にミミックはいない'
    1914: '右にミミックがいる'
    1924: '右にミミックはいない'
  '黒い宝箱の':
    2011: '上にミミックがいる'
    2021: '上にミミックはいない'
    2012: '下にミミックがいる'
    2022: '下にミミックはいない'
    2013: '左にミミックがいる'
    2023: '左にミミックはいない'
    2014: '右にミミックがいる'
    2024: '右にミミックはいない'
  'ミミックの':
    2111: '上に赤い宝箱がある'
    2211: '上に青い宝箱がある'
    2311: '上に黒い宝箱がある'
    2112: '下に赤い宝箱がある'
    2212: '下に青い宝箱がある'
    2312: '下に黒い宝箱がある'
    2113: '左に赤い宝箱がある'
    2213: '左に青い宝箱がある'
    2313: '左に黒い宝箱がある'
    2114: '右に赤い宝箱がある'
    2214: '右に青い宝箱がある'
    2314: '右に黒い宝箱がある'

  '赤の宝箱のどこかに':
    3002: '装備品が入っている'
    3003: 'お金が入っている'
    3004: '消費アイテムが入っている'
  '赤の宝箱には':
    3102: '装備品は入っていない'
    3103: 'お金は入っていない'
    3104: '消費アイテムは入っていない'
  '青の宝箱のどこかに':
    4002: '装備品が入っている'
    4003: 'お金が入っている'
    4004: '消費アイテムが入っている'
  '青の宝箱には':
    4102: '装備品は入っていない'
    4103: 'お金は入っていない'
    4104: '消費アイテムは入っていない'
  '黒の宝箱のどこかに':
    5002: '装備品が入っている'
    5003: 'お金が入っている'
    5004: '消費アイテムが入っている'
  '黒の宝箱には':
    5102: '装備品は入っていない'
    5103: 'お金は入っていない'
    5104: '消費アイテムは入っていない'
  'この中にミミックは':
    50001: '1匹いる'
    50002: '2匹いる'
    50003: '3匹いる'
    50004: '4匹いる'
    50005: '5匹いる'
    50006: '6匹いる'
    50007: '7匹いる'
    50008: '8匹いる'
    50009: '9匹いる'

window.CACHE =
  TD_HTML: null

window.COND2IMAGE_FILE = {}

$().ready ->
  init()
  initImage()
  debug()

initImage = ->
  images = {}
  onload = (index, img)->
    window.COND2IMAGE_FILE[index] = new ImageFileMimicLogic(img, ImageFileMimicLogic.MODE.IMAGE)
  onerror = (index)->
    window.COND2IMAGE_FILE[index] = null
  for key, val of window.CONDS
    for condIndex, condText of val
      image = new Image()
      image.onload = onload.bind(image, condIndex, image)
      image.onerror = onerror.bind(image, condIndex)
      image.src = './image/conds/'+condIndex+'.png'

onPasteImage = (e)->
  # ブラウザによる貼り付けは無効
  e.preventDefault()

  console.log '画像貼付:', e
  clipboardData = e.originalEvent.clipboardData
  return true if !clipboardData
  return true if !clipboardData.types
  indexOfFiles = false
  for type, index in clipboardData.types
    if type is 'Files'
      indexOfFiles = index
      break
  return true if indexOfFiles is false

  imageFile = clipboardData.items[indexOfFiles].getAsFile()
  fr = new FileReader
  fr.onload = (e) ->
    base64 = e.target.result
    #window.parseImage base64
    window.parseImageDebug base64
  fr.readAsDataURL imageFile
  return true

parseImage = (base64)->



cond2cond1 = (cond)->
  for cond1, obj of window.CONDS
    for id, cond2japanese of obj
      return cond1 if Number(id) is Number(cond)
  null

init = ->
  $(':checkbox').radiocheck()

  $('#image_paste').on 'paste', onPasteImage
  $('#item_detail_on').on 'change', viewDetail

  $('#num_x, #num_y').html('')
  for index in [window.CONFIG.FIELD_MIN..window.CONFIG.FIELD_MAX]
    $('#num_x, #num_y').append($('<option>').attr('value', index).html(index))
  $('#num_x, #num_y').val(2)

  $('#num_x, #num_y').on 'change', reset
  $('#judge').on 'click', judge
  $('#clear').on 'click', clear
  $('#num_mimic_min, #num_mimic_max, #num_mad, #num_money, #num_equip, #num_commodity').on 'change', judge
  reset()
  viewDetail()

viewDetail = ->
  if isItemDetail()
    $('#item_detail').removeClass 'no_display'
  else
    $('#item_detail').addClass 'no_display'

clear = ->
  numMimicMin = $('#num_mimic_min').val()
  numMimicMax = $('#num_mimic_max').val()
  numMad = $('#num_mad').val()
  reset()
  $('#num_mimic_min').val(numMimicMin)
  $('#num_mimic_max').val(numMimicMax)
  $('#num_mad').val(numMad)

reset = ->
  x = getX()
  y = getY()
  cellNum = getCellNum()

  $('#num_mimic_min, #num_mimic_max, #num_mad, #num_money, #num_equip, #num_commodity').html('')
  $('#field tbody').html('')
  for yIndex in [0...y]
    tr = $('<tr>')
    for xIndex in [0...x]
      index = yIndex*x+xIndex
      tr.append getTd(index)
      $('#num_mimic_min, #num_mimic_max, #num_mad, #num_money, #num_equip, #num_commodity').append($('<option>').html(index).attr('value', index))
    $('#field tbody').append tr
  $('#field tbody').find('.box').selectpicker({
    noneSelectedText: ''
    width: 'fit'
  })
  $('#num_mimic_min, #num_mimic_max, #num_mad, #num_money, #num_equip, #num_commodity').append($('<option>').html(cellNum).attr('value', cellNum))

getTd = (index)->
  if window.CACHE.TD_HTML is null
    window.CACHE.TD_HTML = $('#cell_sample').html()
  td = $('<td>').html(window.CACHE.TD_HTML).addClass('cell center').attr('data-index', index)
  td.find('.cond1, .cond2').html('')
  td.find('.cond1').append $('<option>').html('').attr('value', 0)
  td.find('.cond2').on 'change', judge


  options = []
  for title, obj of window.CONDS
    options.push $('<option>').html(title)
  options.sort (a, b)->
    return -1 if a.text() < b.text()
    return 1 if a.text() > b.text()
    0
  td.find('.cond1').append op for op in options
  td.find('.cond1').on 'change', changeCond2
  td
  
changeCond2 = ->
  cond1 = $(@).val()
  cond2 = $(@).parent().find('.cond2')
  cond2.html('').append($('<option>').html('').attr('value', 0))
  options = []
  for condId, text of window.CONDS[cond1]
    options.push $('<option>').html(text).attr('value', condId)
  options.sort (a, b)->
    return -1 if a.text() < b.text()
    return 1 if a.text() > b.text()
    0
  cond2.append op for op in options

judge = ->
  viewTitle(null)
  x = getX()
  y = getY()
  cellNum = getCellNum()
  numMimicMin = getNumMimicMin()
  numMimicMax = getNumMimicMax()
  numMad = getNumMad()
  numMoney = getNumMoney()
  numEquip = getNumEquip()
  numCommodity = getNumCommodity()
  nums = {}
  nums[window.CONSTS.MIMIC] = [numMimicMin, numMimicMax]
  nums[window.CONSTS.EQUIP] = numEquip
  nums[window.CONSTS.MONEY] = numMoney
  nums[window.CONSTS.COMMODITY] = numCommodity

  conds = Utl.arrayFill getCellNum()
  colors = Utl.arrayFill getCellNum()
  $('.cell').each ->
    index = Number $(@).attr('data-index')
    box = Number $(@).find('select.box').eq(0).val()
    box = if Utl.inArray(box, [window.COLORS.RED, window.COLORS.BLUE, window.COLORS.BLACK]) then box else window.COLORS.RED
    cond = Number $(@).find('.cond2').eq(0).val()
    conds[index] = cond
    colors[index] = box
  # インデックス一覧
  stockedIndexes = 
    COLOR: getColorIndexes colors # 色ごと
    LINE: getLineIndexes colors, x, y # 列ごと
    NEAR: getNearIndexes colors, x, y # 自身の隣
    COLOR_NEAR: getColorNearIndexes colors, x, y  # 色の隣
  console.log 'conds:', conds
  console.log 'colors:', colors
  console.log 'stockedIndexes', stockedIndexes

  # まずはミミックだけを仮定する
  condsLight = Utl.clone conds
  # 条件を緩める
  for condsIndex in [0...condsLight.length]
    if mustConsiderType([condsLight[condsIndex]])
      condsLight[condsIndex] = 0

  # 確定パターン取得
  confirms = getConfirmTypes(true)
  for value, index in confirms
    if value isnt null and value isnt 0
      confirms[index] = [value]
    else
      confirms[index] = null

  patterns = []
  for numMimic in [numMimicMin..numMimicMax]
    patterns = patterns.concat genPattern([cellNum - numMimic, numMimic, 0, 0, 0], confirms)
  validsOnlyMimicResearch = getValidPatterns(condsLight, colors, stockedIndexes, patterns, nums, numMad)
  [validPatternOnlyMimicResearch, madPatternOnlyMimicResearch] = statValidPatterns(validsOnlyMimicResearch)
  console.log 'validsOnlyMimicResearch, validPatternOnlyMimicResearch, madPatternOnlyMimicResearch', validsOnlyMimicResearch, validPatternOnlyMimicResearch, madPatternOnlyMimicResearch
  # アイテム種別を考慮する必要がないか、この時点でミミックが確定できていれば終了
  if not isItemDetail() or not mustConsiderType(conds) or checkMimic(validPatternOnlyMimicResearch, numMimic)
    console.log 'finished.1.'
    return viewTitle validPatternOnlyMimicResearch, madPatternOnlyMimicResearch

  # より詳細に見る
  # ありえるtypeCostを作る
  confirms = getConfirmTypes(false)
  allowed = Utl.arrayFill confirms.length, []
  kakuteiTypes = []
  kakuteiTypes[val] = 0 for val, key of window.CONSTS
  for dummy, index in allowed
    # 規定されてる
    if confirms[index] isnt 0 and confirms[index] isnt null
      allowed[index].push confirms[index]
      kakuteiTypes[confirms[index]]++
    # 先の結果から分かる
    else
      if Utl.inArray(window.CONSTS.MIMIC, validPatternOnlyMimicResearch[index])
        allowed[index].push window.CONSTS.MIMIC
      if Utl.inArray(window.CONSTS.NOT_MIMIC, validPatternOnlyMimicResearch[index])
        allowed[index].push window.CONSTS.EQUIP, window.CONSTS.MONEY, window.CONSTS.COMMODITY

  valids = []
  for validOnlyMimicResearch in validsOnlyMimicResearch
    numMimic = 0
    for typeConst in validOnlyMimicResearch
      numMimic++ if typeConst is window.CONSTS.MIMIC

    # アイテム詳細の指定があるので決め打ち
    if isItemDetail()
      patterns = genPattern([0, numMimic, numEquip, numMoney, numCommodity], allowed)
      nums = {}
      nums[window.CONSTS.MIMIC] = [numMimic, numMimic]
      nums[window.CONSTS.EQUIP] = numEquip
      nums[window.CONSTS.MONEY] = numMoney
      nums[window.CONSTS.COMMODITY] = numCommodity
      valids = valids.concat getValidPatterns(conds, colors, stockedIndexes, patterns, nums, numMad)
      
    # アイテム詳細の指定がない場合は全パターン
    else
      for numEquip in [0..(cellNum - numMimic)]
        continue if numEquip < kakuteiTypes[window.CONSTS.EQUIP]
        for numMoney in [0..(cellNum - numMimic)]
          continue if numMoney < kakuteiTypes[window.CONSTS.MONEY]
          for numCommodity in [0..(cellNum - numMimic)]
            continue if numCommodity < kakuteiTypes[window.CONSTS.COMMODITY]
            continue unless (numMimic + numEquip + numMoney + numCommodity) is cellNum
            patterns = genPattern([0, numMimic, numEquip, numMoney, numCommodity], allowed)
            nums = {}
            nums[window.CONSTS.MIMIC] = [numMimic, numMimic]
            nums[window.CONSTS.EQUIP] = numEquip
            nums[window.CONSTS.MONEY] = numMoney
            nums[window.CONSTS.COMMODITY] = numCommodity
            valids = valids.concat getValidPatterns(conds, colors, stockedIndexes, patterns, nums, numMad)
  [validPattern, madPattern] = statValidPatterns(valids)
  console.log 'valids, validPattern, madPattern', valids, validPattern, madPattern
  # 全探索したので最終回答
  viewTitle validPattern
  window.alert '条件を満たすパターンがありません' if validPattern is null
  console.log 'finished.2.'
  
viewTitle = (views = null, mads = null)->
  $('td, .title, .title_mad').removeClass('not_mimic mimic money equip commodity unknown')
  $('.title, .title_mad').html('')
  return if views is null

  for typeConsts, index in views
    [className, html] = 
      if typeConsts.length > 1
        if Utl.inArray(window.CONSTS.MIMIC, typeConsts)
          ['unknown', '？']
        else
          ['not_mimic', '宝']
      else
        switch typeConsts[0]
          when window.CONSTS.MIMIC
            ['mimic', 'ミミック']
          when window.CONSTS.NOT_MIMIC
            ['not_mimic', '宝']
          when window.CONSTS.EQUIP
            ['equip', '装備']
          when window.CONSTS.MONEY
            ['money', 'お金']
          when window.CONSTS.COMMODITY
            ['commodity', '消費アイテム']
          else
            ['', '']
    $('.title').eq(index).addClass(className).html(html)
    $('td').eq(index).addClass(className)

  # 狂った宝箱判定
  if mads isnt null
    for madArray, index in mads
      if madArray.length is 1 and madArray[0]
        $('.title_mad').eq(index).addClass('mimic').html('狂った宝箱')


# 宝の種別を考慮しないといけないか
mustConsiderType = (conds)->
  for cond in conds
    return true if 732 <= cond <= 744
    return true if 832 <= cond <= 844
    return true if 932 <= cond <= 944
    return true if 1032 <= cond <= 1044
    return true if 10002 <= cond <= 10014
    return true if 20002 <= cond <= 20014
    return true if 30002 <= cond <= 30014
    return true if 40002 <= cond <= 40014
    return true if 3002 <= cond <= 5104
  false


checkMimic = (validPattern, numMimicMin, numMimicMax)->
  mimic = 0
  for typeConstArray, index in validPattern
    mimic++ if typeConstArray.length is 1 and typeConstArray[0] is window.CONSTS.MIMIC
  console.log 'checkMimic:', numMimicMin <= mimic <= numMimicMax
  numMimicMin <= mimic <= numMimicMax


getValidPatterns = (conds, colors, stockedIndexes, patterns, nums, numMad)->
  valids = []
  for pattern in patterns
    # 狂った宝箱がある場合は、狂った宝箱のパターンも全部試す
    if numMad > 0
      madPatterns = getPatternMad(numMad, pattern)
      for madPattern in madPatterns
        res = isValidPattern(conds, colors, stockedIndexes, pattern, nums, madPattern)
        valids.push [pattern, madPattern] if res
    # 狂った宝箱がないから普通に調べる
    else
      res = isValidPattern(conds, colors, stockedIndexes, pattern, nums)
      valids.push [pattern, null] if res
  valids

statValidPatterns = (validPatterns)->
  collects = []
  mads = []
  for [valid, madPattern] in validPatterns
    for type, index in valid
      unless collects[index]?
        collects[index] = []
      unless mads[index]?
        mads[index] = []
      if not Utl.inArray type, collects[index]
        collects[index].push type
      # 狂った宝箱の可能性
      if madPattern isnt null and not Utl.inArray madPattern[index], mads[index]
        mads[index].push madPattern[index]
  [collects, mads]

downDementionStat = (stat)->
  for ary, index in stat
    if ary.length is 1
      stat[index] = ary[0]
    else
      stat[index] = null
  ary


isValidPattern = (conds, colors, stockedIndexes, pattern, nums, madPattern = null)->
  console.log('conds, pattern, madPattern', conds, pattern, madPattern)
  for cond, index in conds
    isMimic = pattern[index] is window.CONSTS.MIMIC
    isMad = madPattern isnt null and madPattern[index]
    isTurn = isMimic or isMad # 最終的に反転するか
    res = switch cond
      # ある宝箱の隣にミミックがいる
      when 710,810,910,1010 then isContainType(pattern, stockedIndexes.NEAR[index][cond // 100 % 6], window.CONSTS.MIMIC, nums)
      # ある宝箱の隣にミミックがいない
      when 720,820,920,1020 then isNotContainType(pattern, stockedIndexes.NEAR[index][cond // 100 % 6], window.CONSTS.MIMIC, nums)

      # ある色の宝箱にミミックがいる
      when 10,110,210 then isContainType(pattern, stockedIndexes.COLOR[cond // 100 + 1], window.CONSTS.MIMIC, nums)

      # ある色の宝箱にミミックがn匹いる
      when 30,31,32,33,34,35,36,37,38,39,130,131,132,133,134,135,136,137,138,139,230,231,232,233,234,235,236,237,238,239 then isContainTypeCount(pattern, stockedIndexes.COLOR[cond // 100 + 1], window.CONSTS.MIMIC, cond % 10, nums)

      # ある色の宝箱にミミックがいない
      when 20,120,220 then isNotContainType(pattern, stockedIndexes.COLOR[cond // 100 + 1], window.CONSTS.MIMIC, nums)

      # ある色の宝箱に特定のアイテムが入っている
      when 3002,3003,3004,4002,4003,4004,5002,5003,5004 then isContainType(pattern, stockedIndexes.COLOR[cond // 1000 - 2], cond % 10, nums)
      # ある色の宝箱に特定のアイテムが入っていない
      when 3102,3103,3104,4102,4103,4104,5102,5103,5104 then isNotContainType(pattern, stockedIndexes.COLOR[cond // 1000 - 2], cond % 10, nums)

      # ある列にミミックがいる
      when 310,410,510,610 then isContainType(pattern, stockedIndexes.LINE[cond // 100 - 2], window.CONSTS.MIMIC, nums)

      # ある列にミミックがいない
      when 320,420,520,620 then isNotContainType(pattern, stockedIndexes.LINE[cond // 100 - 2], window.CONSTS.MIMIC, nums)

      # ある列に特定のアイテムが入っている
      when 10002,10003,10004,20002,20003,20004,30002,30003,30004,40002,40003,40004 then isContainType(pattern, stockedIndexes.LINE[cond // 10000], cond % 10, nums)
      # ある列に特定のアイテムが入っていない
      when 10012,10013,10014,20012,20013,20014,30012,30013,30014,40012,40013,40014 then isNotContainType(pattern, stockedIndexes.LINE[cond // 10000], cond % 10, nums)

      # ミミック同士は隣り合った位置に
      # いる
      when 1100 then isMimicNearly(pattern, stockedIndexes.NEAR)
      # いない
      when 1110 then not isMimicNearly(pattern, stockedIndexes.NEAR)
      
      # 上の列と下の列は
      # 上の列の方がミミックが多い
      when 1210 then compareIndexes(pattern, stockedIndexes.LINE[window.DIRECTIONS.UP], stockedIndexes.LINE[window.DIRECTIONS.DOWN], -1)
      # 下の列の方がミミックが多い
      when 1220 then compareIndexes(pattern, stockedIndexes.LINE[window.DIRECTIONS.UP], stockedIndexes.LINE[window.DIRECTIONS.DOWN], 1)
      # ミミックの数は同じ
      when 1230 then compareIndexes(pattern, stockedIndexes.LINE[window.DIRECTIONS.UP], stockedIndexes.LINE[window.DIRECTIONS.DOWN], 0)

      # 左の列と右の列は
      # 左の列の方がミミックが多い
      when 1310 then compareIndexes(pattern, stockedIndexes.LINE[window.DIRECTIONS.LEFT], stockedIndexes.LINE[window.DIRECTIONS.RIGHT], -1)
      # 右の列の方がミミックが多い
      when 1320 then compareIndexes(pattern, stockedIndexes.LINE[window.DIRECTIONS.LEFT], stockedIndexes.LINE[window.DIRECTIONS.RIGHT], 1)
      # ミミックの数は同じ
      when 1330 then compareIndexes(pattern, stockedIndexes.LINE[window.DIRECTIONS.LEFT], stockedIndexes.LINE[window.DIRECTIONS.RIGHT], 0)

      # 赤宝箱と青宝箱は
      # 赤宝箱の方がミミックが多い
      when 1410, 1520 then compareIndexes(pattern, stockedIndexes.COLOR[window.COLORS.RED], stockedIndexes.COLOR[window.COLORS.BLUE], -1)
      # 青宝箱の方がミミックが多い
      when 1420, 1510 then compareIndexes(pattern, stockedIndexes.COLOR[window.COLORS.RED], stockedIndexes.COLOR[window.COLORS.BLUE], 1)
      # 同じ
      when 1421, 1521 then compareIndexes(pattern, stockedIndexes.COLOR[window.COLORS.RED], stockedIndexes.COLOR[window.COLORS.BLUE], 0)
      # 赤宝箱と黒宝箱は
      # 赤宝箱の方がミミックが多い
      when 1430, 1620 then compareIndexes(pattern, stockedIndexes.COLOR[window.COLORS.RED], stockedIndexes.COLOR[window.COLORS.BLACK], -1)
      # 黒宝箱の方がミミックが多い
      when 1440, 1610 then compareIndexes(pattern, stockedIndexes.COLOR[window.COLORS.RED], stockedIndexes.COLOR[window.COLORS.BLACK], 1)
      # 同じ
      when 1441, 1621 then compareIndexes(pattern, stockedIndexes.COLOR[window.COLORS.RED], stockedIndexes.COLOR[window.COLORS.BLACK], 0)
      # 青宝箱と黒宝箱は
      # 青宝箱の方がミミックが多い
      when 1530, 1640 then compareIndexes(pattern, stockedIndexes.COLOR[window.COLORS.BLUE], stockedIndexes.COLOR[window.COLORS.BLACK], -1)
      # 黒宝箱の方がミミックが多い
      when 1540, 1630 then compareIndexes(pattern, stockedIndexes.COLOR[window.COLORS.BLUE], stockedIndexes.COLOR[window.COLORS.BLACK], 1)
      # 同じ
      when 1541, 1641 then compareIndexes(pattern, stockedIndexes.COLOR[window.COLORS.BLUE], stockedIndexes.COLOR[window.COLORS.BLACK], 0)

      # 私はミミックじゃない
      when 1700 then isNotContainType(pattern, [index], window.CONSTS.MIMIC, nums)

      # どこかの方向は特定の宝だ
      when 732,733,734,832,833,834,932,933,934 then isContainType(pattern, stockedIndexes.NEAR[index][cond // 100 - 6], cond % 10, nums)
      # どこかの方向は特定の宝ではない
      when 742,743,744,842,843,844,942,943,944 then isNotContainType(pattern, stockedIndexes.NEAR[index][cond // 100 - 6], cond % 10, nums)

      # 特定の色の宝箱の
      # どこかにミミックがいる
      when 1811,1812,1813,1814,1911,1912,1913,1914,2011,2012,2013,2014 then isContainType(pattern, stockedIndexes.COLOR_NEAR[Math.floor(cond / 100) % 17][cond % 10], window.CONSTS.MIMIC, nums)
      when 1821,1822,1823,1824,1921,1922,1923,1924,2021,2022,2023,2024 then isNotContainType(pattern, stockedIndexes.COLOR_NEAR[Math.floor(cond / 100) % 17][cond % 10], window.CONSTS.MIMIC, nums)

      # ミミックの
      # どこかにどれかの色がある
      when 2111,2112,2113,2114,2211,2212,2213,2214,2311,2312,2313,2314 then isContainColorNearMimic(pattern, colors, stockedIndexes.NEAR, cond % 10, Math.floor(cond / 100) % 20)

      # この中にミミックはn匹いる
      when 50001,50002,50003,50004,50005,50006,50007,50008,50009 then isContainTypeCount(pattern, [0...pattern.length], window.CONSTS.MIMIC, cond % 10, nums)

      else 
        isTurn = false
        true
    
    # ミミックか狂った宝箱なら反転
    if isTurn
      res = not res 
    console.log('cond, condBool, isMimic, isMad', cond, res, isMimic, isMad)
    return false unless res
  true

# [win]
# 0 : 同じ
# -1: indexes1が多い
# 1 : indexes2が多い
compareIndexes = (pattern, indexes1, indexes2, win)->
  mimic1 = mimic2 = 0
  for index in indexes1
    mimic1++ if pattern[index] is window.CONSTS.MIMIC
  for index in indexes2
    mimic2++ if pattern[index] is window.CONSTS.MIMIC
  # line1が多い
  if win < 0
    mimic1 > mimic2
  # line2が多い
  else if win > 0
    mimic1 < mimic2
  else
    mimic1 is mimic2


# ミミック同士は隣あった位置にいるか
isMimicNearly = (pattern, nearIndexes)->
  # 1体しかいないなら偽
  return false if getMimicIndexes(pattern).length is 1

  mimics = []
  for typeConst, index in pattern
    mimics.push index if typeConst is window.CONSTS.MIMIC
  for index1 in [0...mimics.length]
    for index2 in [index1+1...mimics.length]
      for key, indexes of nearIndexes[mimics[index1]]
        return true if Utl.inArray(mimics[index2], indexes)
  false

# 指定したインデックスに指定したタイプがいるか
isContainType = (pattern, indexes, typeConst, nums)->
  # 存在しない
  if Utl.inArray(typeConst, [window.CONSTS.MONEY, window.CONSTS.EQUIP, window.CONSTS.COMMODITY])
    return false if nums[typeConst] is 0
  for index in indexes
    # ミミック以外を指定で、ミミックではない
    return true if typeConst is window.CONSTS.NOT_MIMIC and pattern[index] isnt window.CONSTS.MIMIC
    # ミミック以外の特定のアイテムを指定で、ミミックではない
    return true if Utl.inArray(typeConst, [window.CONSTS.MONEY, window.CONSTS.EQUIP, window.CONSTS.COMMODITY]) and pattern[index] is window.CONSTS.NOT_MIMIC
    # タイプ合致
    return true if typeConst is pattern[index]
  false

# 指定したインデックスに指定したタイプがn匹いるか
isContainTypeCount = (pattern, indexes, typeConst, count, nums)->
  # そんなに存在しえない
  if Utl.inArray(typeConst, [window.CONSTS.MONEY, window.CONSTS.EQUIP, window.CONSTS.COMMODITY])
    return false if nums[typeConst] < count
  if typeConst is window.CONSTS.MIMIC
    return false if nums[typeConst][1] < count

  nowCount = 0
  for index in indexes
    if typeConst is window.CONSTS.NOT_MIMIC and pattern[index] isnt window.CONSTS.MIMIC
      nowCount++
    else if typeConst is pattern[index]
      nowCount++
      
  count is nowCount

# 指定したインデックスに指定したタイプがいないか
isNotContainType = (pattern, indexes, typeConst, nums)->
  # 存在しない
  if Utl.inArray(typeConst, [window.CONSTS.MONEY, window.CONSTS.EQUIP, window.CONSTS.COMMODITY])
    return true if nums[typeConst] is 0
  if typeConst is window.CONSTS.MIMIC
    return true if nums[typeConst][0] is 0 and nums[typeConst][1] is 0

  for index in indexes
    # ミミック以外が含まれていないのに、ミミック以外が含まれている
    return false if typeConst is window.CONSTS.NOT_MIMIC and pattern[index] isnt window.CONSTS.MIMIC
    # ミミック以外の特定のアイテムを指定で、ミミックではない
    return false if Utl.inArray(typeConst, [window.CONSTS.MONEY, window.CONSTS.EQUIP, window.CONSTS.COMMODITY]) and pattern[index] is window.CONSTS.NOT_MIMIC
    # ミミックが含まれていないのに、ミミックである
    return false if typeConst is window.CONSTS.MIMIC and pattern[index] is window.CONSTS.MIMIC
    # タイプ合致
    return false if typeConst is pattern[index]
  true

# ミミックの隣接に特定の色があるか
isContainColorNearMimic = (pattern, colors, nearIndexes, direction, color)->
  mimicNearIndexes = getMimicNearIndexes pattern, nearIndexes, direction
  for mimicNearIndex in mimicNearIndexes
    return true if colors[mimicNearIndex] is color
  false

getMimicIndexes = (pattern)->
  res = []
  for typeConst, index in pattern
    res.push index if typeConst is window.CONSTS.MIMIC
  res

# ミミックの隣接マスを返す
getMimicNearIndexes = (pattern, nearIndexes, direction)->
  res = []
  mimicIndexes = getMimicIndexes pattern
  for mimicIndex in mimicIndexes
    for index in nearIndexes[mimicIndex][direction]
      res.push index unless Utl.inArray index, res
  res

# 特定の色の宝箱のインデックス
getColorIndexes = (colors)->
  res = {}
  for name, num of window.COLORS
    res[num] = []
  for color, index in colors
    res[color].push index
  res

# 特定の列のインデックス
getLineIndexes = (colors, x, y)->
  res = {}
  for dirName, dirNum of window.DIRECTIONS
    res[dirNum] = []
  # 上
  res[window.DIRECTIONS.UP].push index for index in [0...x]
  # 下
  res[window.DIRECTIONS.DOWN].push index for index in [x*y-x...x*y]
  # 左
  res[window.DIRECTIONS.LEFT].push index*x for index in [0...y]
  # 右
  res[window.DIRECTIONS.RIGHT].push (index+1)*x-1 for index in [0...y]

  res

# 特定の宝箱の隣接のインデックス
getNearIndexes = (colors, x, y)->
  res = Utl.arrayFill colors.length, {}
  for index in [0...res.length]
    for dirName, dirNum of window.DIRECTIONS
      res[index][dirNum] = []
  for color, index in colors
    # 上
    up = index - x
    res[index][window.DIRECTIONS.UP].push up if 0 <= up < x*y
    # 下
    down = index + x
    res[index][window.DIRECTIONS.DOWN].push down if 0 <= down < x*y
    # 左
    left = index - 1
    res[index][window.DIRECTIONS.LEFT].push left if 0 <= left < x*y and index // x is left // x
    # 右
    right = index + 1
    res[index][window.DIRECTIONS.RIGHT].push right if 0 <= right < x*y and index // x is right // x
  res

# 特定の色の宝箱の隣接のインデックス
getColorNearIndexes = (colors, x, y)->
  res = {}
  for colorName, colorNum of window.COLORS
    res[colorNum] = {}
    for dirName, dirNum of window.DIRECTIONS
      res[colorNum][dirNum] = []
  for color, index in colors
    # 上
    up = index - x
    res[color][window.DIRECTIONS.UP].push up if 0 <= up < x*y
    # 下
    down = index + x
    res[color][window.DIRECTIONS.DOWN].push down if 0 <= down < x*y
    # 左
    left = index - 1
    res[color][window.DIRECTIONS.LEFT].push left if 0 <= left < x*y and index // x is left // x
    # 右
    right = index + 1
    res[color][window.DIRECTIONS.RIGHT].push right if 0 <= right < x*y and index // x is right // x
  res

getX = ->
  Number $('#num_x').val()
getY = ->
  Number $('#num_y').val()
getCellNum = ->
  getX() * getY()
getNumMimicMin = ->
  Number $('#num_mimic_min').val()
getNumMimicMax = ->
  Number $('#num_mimic_max').val()
getNumMad = ->
  Number $('#num_mad').val()
isItemDetail = ->
  $('#item_detail_on').prop 'checked'
getNumMoney = ->
  Number $('#num_money').val()
getNumEquip = ->
  Number $('#num_equip').val()
getNumCommodity = ->
  Number $('#num_commodity').val()
condId2Text = (condId)->
  for jap1, val of window.CONDS
    for id, jap2 of val
      return jap1+jap2 if Number(id) is Number(condId)
  null
# 確定パターンの取得
getConfirmTypes = (modeNotMimic)->
  confirms = []
  $('#field tbody').find('select.kind').each ->
    val = Number $(@).val()
    if val is 0
      confirms.push null
    else if modeNotMimic and val isnt window.CONSTS.MIMIC
      confirms.push 0
    else
      confirms.push val
  confirms

genPattern = (ary, pattern = null)->
  # [ミミック以外(オールマイティ), ミミック, 装備, お金, 消費アイテム]
  consts = [window.CONSTS.NOT_MIMIC, window.CONSTS.MIMIC, window.CONSTS.EQUIP, window.CONSTS.MONEY, window.CONSTS.COMMODITY]
  remain = Utl.clone ary
  total = 0
  total += num for num in ary

  all = []
  getPatternFunc = (m, remain)->
    if m.length is total
      all.push m
      return true
    for num, index in remain
      continue if num <= 0
      # 指定のもの以外は飛ばす
      if pattern isnt null
        if pattern[m.length] isnt null and pattern[m.length].length > 0 and not Utl.inArray consts[index], pattern[m.length]
          continue

      newM = Utl.clone m
      newRemain = Utl.clone remain
      newRemain[index]--
      newM.push consts[index]
      getPatternFunc(newM, newRemain)

  getPatternFunc([], remain)
  console.log 'genPattern 組み合わせ:', ary, pattern, all
  all

getPatternMad = (numMad, pattern)->
  # 狂った宝箱なし
  return null if numMad <= 0

  all = []
  getPatternFunc = (m, remain)->
    if m.length is pattern.length
      all.push m if remain is 0
      return true
    for bool in [true, false]
      continue if bool and remain <= 0
      continue if bool and pattern[m.length] is window.CONSTS.MIMIC
      newM = Utl.clone m
      newRemain = if bool then remain - 1 else remain
      newM.push bool
      getPatternFunc(newM, newRemain)

  getPatternFunc([], numMad)
  console.log 'getPatternMad 組み合わせ:', numMad, pattern, all
  all